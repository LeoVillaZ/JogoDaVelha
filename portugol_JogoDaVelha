[Portugol]

Algoritmo "JogoDaVelha"
// Disciplina   : Algorítimo
// Professor   : Gustavo Guanabrada
// Descrição   : Jogo da Velha
// Autor(a)    : Leandro Vilela
// Data atual  : 19/08/2020
Var
   // Seção de Declarações das variáveis

   grade: vetor[1..3,1..3] de Caracter
   linha, coluna, contador, posicao: Inteiro
   simbulo: caracter
   resposta: Logico

Procedimento MostraVelha()
Inicio
   EscrevaL("+---+---+---+")
   Para linha <- 1 ate 3 faca
      Para coluna <- 1 ate 3 faca
         Escreva("|  ", grade[linha,coluna]:1)
      FimPara
      Escreva("|")
      EscrevaL()
      EscrevaL("+---+---+---+")
   FimPara
FimProcedimento


Procedimento MudaJogador()
inicio
   Se (simbulo = "X") entao
      simbulo <- "O"
   senao
      simbulo <- "X"
   FimSe
FimProcedimento

Funcao Jogar(S: Caracter; P: Inteiro): Logico
var
   mudou: logico
inicio
   mudou <- falso
   Para linha <- 1 ate 3 faca
      Para coluna <- 1 ate 3 faca
         Se (grade[linha,coluna] = NumpCarac(P)) entao
            grade[linha,coluna] <- S
            mudou <- verdadeiro
         FimSe
      FimPara
   FimPara
   Retorne mudou
FimFuncao

Funcao TerminouVelha(): Logico
Var
   terminou: Logico
   ocorr: Inteiro
inicio
   terminou <- falso
   //Jogos em linha
   Para linha <- 1 ate 3 faca
      Se (grade[linha,1] = grade[linha,2]) e (grade[linha,2] = grade[linha,3]) entao
         terminou <- verdadeiro
      FimSe
   FimPara

   //Jogos em Coluna
   Para coluna <- 1 ate 3 faca
      Se (grade[1,coluna] = grade[2,coluna]) e (grade[2,coluna] = grade[3,coluna]) entao
         terminou <- verdadeiro
      FimSe
   FimPara

   //Jogos em Diagonal
   Se (grade[1,1] = grade[2,2]) e (grade[2,2] = grade[3,3]) entao
      terminou <- verdadeiro
   FimSe

   Se (grade[1,3] = grade[2,2]) e (grade[2,2] = grade[3,1]) entao
      terminou <- verdadeiro
   FimSe

   //Jogos em Velha
   ocorr <- 0
   Para linha <- 1 ate 3 faca
      Para coluna <- 1 ate 3 faca
         Se (grade[linha,coluna] <> "X") e (grade[linha,coluna] <> "O") entao
            ocorr <- ocorr + 1
         FimSe
      FimPara
   FimPara
   Se (ocorr = 0) entao
      terminou <- verdadeiro
   FimSe

   //Retorna se o jogo acabou ou não
   Retorne terminou
FimFuncao

Inicio
   // Seção de Comandos, procedimento, funções, operadores, etc...

   contador <- 1
   simbulo <- "X"

   Para linha <- 1 ate 3 faca
      Para coluna <- 1 ate 3 faca
         grade[linha,coluna] <- NumpCarac(contador)
         contador <- contador + 1
      FimPara
   FimPara

   MostraVelha()

   Repita
      Repita
         EscrevaL("Vai jogar [", simbulo:1, "] em qual posição? ")
         Leia(posicao)
         resposta <- Jogar(simbulo, posicao)
         Se (resposta = falso) entao
            EscrevaL ("JOGADA INVÁLIDA!")
         FimSe
      Ate (resposta = verdadeiro)
      MudaJogador()
      LimpaTela
      MostraVelha()
   Ate (TerminouVelha() = verdadeiro)
   EscrevaL("Jogo Finalizado!")

Fimalgoritmo
